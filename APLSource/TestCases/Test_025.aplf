 R←Test_025(stopFlag batchFlag);path;∆;⎕TRAP
⍝ Exercise `PutValue` & `GetValue`
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs

⍝ Tests...
   ⍝ Write a number and read it back
 ##.WinReg.PutValue(path,'number')(∆←123)
   ⍝ Read the data back
 →T.FailsIf ∆≢##.WinReg.GetValue path,'number'

   ⍝ Write invalid data
 :Trap 11
     ##.WinReg.PutValue(path,'number')''
     →0
 :Else
     →T.FailsIf'WinReg error: data has invalid depth/shape'≢0⊃⎕DM
 :EndTrap

 :Trap 11
     ##.WinReg.PutValue(path,'number')'STRING'
     →0
 :Else
     →T.FailsIf'WinReg error: data has invalid depth/shape'≢0⊃⎕DM
 :EndTrap

 :Trap 11
     ##.WinReg.PutValue(path)(123)
     →0
 :Else
     →T.FailsIf'WinReg error: default values must be of type REG_SZ (string)'≢0⊃⎕DM
 :EndTrap

⍝ Tidy up
 ##.WinReg.DeleteSubKeyTree path

 R←T._OK
