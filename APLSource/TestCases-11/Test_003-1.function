 R←Test_003(stopFlag batchFlag);path;rc;buf;⎕TRAP
⍝ Exercise `DeleteSubKeyTree`
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs
 ##.WinReg.PutString(path,'AAA\')'It''s folder AAA'
 ##.WinReg.PutString(path,'AAA\hello')'world'
 ##.WinReg.PutString(path,'AAA\BB\hello')'Universe'
 ##.WinReg.PutString(path,'AAA\BB\')'It''s folder BB'

 buf←##.WinReg.GetString path,'AAA\'
 →FailsIf'It''s folder AAA'≢buf
 buf←##.WinReg.GetString path,'AAA\hello'
 →FailsIf'world'≢buf
 buf←##.WinReg.GetString path,'AAA\BB\'
 →FailsIf'It''s folder BB'≢buf
 buf←##.WinReg.GetString path,'AAA\BB\hello'
 →FailsIf'Universe'≢buf

⍝ Tests...
 rc←##.WinReg.DeleteSubKey path,'AAA'                    ⍝ This fails because it contains subkeys
 →FailsIf 5≠rc                                          ⍝ Access is denied
 rc←##.WinReg.DeleteSubKeyTree path,'AAA'                ⍝ Deletes the whole tree
 →FailsIf 0≠rc                                          ⍝ Access is denied

 rc←##.WinReg.DoesKeyExist path,'AAA\'
 →FailsIf 0≠rc
 rc←##.WinReg.DoesValueExist path,'AAA\hello'
 →FailsIf 0≠rc
 rc←##.WinReg.DoesKeyExist path,'AAA\BB'
 →FailsIf 0≠rc
 rc←##.WinReg.DoesValueExist path,'AAA\BB\hello'
 →FailsIf 0≠rc

⍝ Tidy up
 rc←##.WinReg.DeleteSubKeyTree path                      ⍝ Deletes the whole tree
 →FailsIf 0≠rc                                          ⍝ Access is denied

 R←∆OK
