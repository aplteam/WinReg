 R←Test_026(stopFlag batchFlag);path;∆;⎕TRAP
⍝ Exercise `PutString` & `GetString`
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs

⍝ Tests...
   ⍝ Write a default value and read it back
 ##.WinReg.PutString(path)(∆←'theDefault')
   ⍝ Read the data back
 →FailsIf ∆≢##.WinReg.GetString path

   ⍝ Write "nested" array and read it back
 ##.WinReg.PutString(path,'MyMulti')(∆←'one' 'two' 'three')
   ⍝ Read the data back
 →FailsIf ∆≢##.WinReg.GetString path,'MyMulti'

    ⍝ Write "nested" array and read it back
 ##.WinReg.REG_MULTI_SZ ##.WinReg.PutString(path,'\MyMulti')(∆←'one' 'two' 'three')
   ⍝ Read the data back
 →FailsIf ∆≢##.WinReg.GetString path,'\MyMulti'

   ⍝ Write a string tht contains "%" and read it back
 ##.WinReg.PutString(path,'\MyWinDir')(∆←'%WINDIR%')
   ⍝ Read the data back
 →FailsIf ∆≢##.WinReg.GetString path,'\MyWinDir'

   ⍝ Write an expandable string and read it back
 ##.WinReg.REG_EXPAND_SZ ##.WinReg.PutString(path,'\MyWinDir')('%WINDIR%')
   ⍝ Read the data back
 →FailsIf ∆≡##.WinReg.GetString path,'\MyWinDir'

   ⍝ Write invalid data
 :Trap 11
     ##.WinReg.PutString(path)(123)
     →0
 :Else
     →FailsIf'WinReg error: invalid "data"'≢0⊃⎕DM
 :EndTrap

 :Trap 11
     ##.WinReg.PutString(path,'MyMulti')(∆←'one' 2 'three')
     →0
 :Else
     →FailsIf'WinReg error: invalid "data"'≢0⊃⎕DM
 :EndTrap

⍝ Tidy up
 ##.WinReg.DeleteSubKeyTree path

 R←∆OK
