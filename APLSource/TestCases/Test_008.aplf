 R←Test_008(stopFlag batchFlag);path;names;names2;data;data2;⎕TRAP;qdmx
⍝ Exercide `GetAllValueNames`
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path         ⍝ Delete any left-overs

 ##.WinReg.PutString(path,'String1')'hello'
 ##.WinReg.PutString(path,'Second')'universe'
 ##.WinReg.PutValue(path,'TheRest')42
 ##.WinReg.PutValue(path,'Biggest')2147483647
 ##.WinReg.PutValue(path,'Smallest')¯2147483648
 ##.WinReg.PutString(path,'ThisIsAnExtremelyLongKeyUsedToSaveAStringInTheWindowsRegistry')'long key'
 ##.WinReg.PutString(path,'Nested_1')(20⍴'George' 'Bush')
 ##.WinReg.PutString(path,'Nested_2')(40⍴'Bill' 'Clinton')
 ##.WinReg.PutString(path,'Nested_3')(70⍴'Barak' 'Obama')

 ##.WinReg.PutString(path,'\sub\Agile')'APL'
 ##.WinReg.PutValue(path,'sub\FirstPrime')2

⍝ Tests...
 names←##.WinReg.GetAllValueNames path
 names2←⊂'String1' 'Second' 'TheRest' 'Biggest' 'Smallest' 'ThisIsAnExtremelyLongKeyUsedToSaveAStringInTheWindowsRegistry' 'Nested_1' 'Nested_2' 'Nested_3'
 →T.FailsIf names≢⊃names2

 data←'verbose'##.WinReg.GetAllValueNames path
 data2←'String1' 'Second' 'TheRest' 'Biggest' 'Smallest' 'ThisIsAnExtremelyLongKeyUsedToSaveAStringInTheWindowsRegistry' 'Nested_1' 'Nested_2' 'Nested_3'
 data2←data2,[1.5]1 1 4 4 4 1 7 7 7
 →T.GoToTidyUp data≢data2

 :Trap 11
     ##.WinReg.GetAllValueNames 123 123
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'WinReg error: invalid right argument'≢qdmx.EM
 :EndTrap

 :Trap 11
     ##.WinReg.GetAllValueNames 123
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'WinReg error: ERROR_INVALID_HANDLE'≢qdmx.EM
 :EndTrap

 R←T._OK

∆TidyUp:
 ##.WinReg.DeleteSubKeyTree path
