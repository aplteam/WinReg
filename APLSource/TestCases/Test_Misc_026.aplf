 R←Test_Misc_026(stopFlag batchFlag);path;∆;⎕TRAP;qdmx
⍝ Exercise `PutString` & `GetString`
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs

⍝ Tests...
   ⍝ Write a default value and read it back
 ##.WinReg.PutString(path)(∆←'theDefault')
   ⍝ Read the data back
 →T.FailsIf ∆≢##.WinReg.GetString path

   ⍝ Write "nested" array and read it back
 ##.WinReg.PutString(path,'MyMulti')(∆←'one' 'two' 'three')
   ⍝ Read the data back
 →T.FailsIf ∆≢##.WinReg.GetString path,'MyMulti'

    ⍝ Write "nested" array and read it back
 ##.WinReg.REG_MULTI_SZ ##.WinReg.PutString(path,'\MyMulti')(∆←'one' 'two' 'three')
   ⍝ Read the data back
 →T.FailsIf ∆≢##.WinReg.GetString path,'\MyMulti'

   ⍝ Write a string tht contains "%" and read it back
 ##.WinReg.PutString(path,'\MyWinDir')(∆←'%WINDIR%')
   ⍝ Read the data back
 →T.FailsIf ∆≢##.WinReg.GetString path,'\MyWinDir'

   ⍝ Write an expandable string and read it back
 ##.WinReg.REG_EXPAND_SZ ##.WinReg.PutString(path,'\MyWinDir')('%WINDIR%')
   ⍝ Read the data back
 →T.FailsIf ∆≡##.WinReg.GetString path,'\MyWinDir'

   ⍝ Write invalid data
 :Trap 11
     ##.WinReg.PutString(path)(123)
     →0
 :Else
     qdmx←⎕DMX
     →T.FailsIf'WinReg error: invalid "data"'≢qdmx.EM
 :EndTrap

 :Trap 11
     ##.WinReg.PutString(path,'MyMulti')(∆←'one' 2 'three')
     →0
 :Else
     qdmx←⎕DMX
     →T.FailsIf'WinReg error: invalid "data"'≢qdmx.EM
 :EndTrap

⍝ Tidy up
 ##.WinReg.DeleteSubKeyTree path

 R←T._OK
