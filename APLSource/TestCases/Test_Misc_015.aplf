 R←Test_Misc_015(stopFlag batchFlag);path;myReg;length;_;∆;⎕TRAP
⍝ Exercise `GetValue` & `PutValue` on SZ
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs

⍝ Tests...
   ⍝ * Unknown String "str1":
 →T.FailsIf'?'≢'?'##.WinReg.GetString path,'str1'

   ⍝ * Unknown Value: "val1"
 →T.FailsIf ¯1≢¯1 ##.WinReg.GetValue path,'val1'

   ⍝ * Write String "str2"
 ##.WinReg.PutString(path,'str2')'It is me!'

   ⍝ * Known String "str2":
 →T.FailsIf'It is me!'≢'?'##.WinReg.GetString path,'str2'

   ⍝ * Read default value of subkey "path" (undefined yet):
 →T.FailsIf'?'≢'?'##.WinReg.GetString path

   ⍝ * Set the default value of subkey "path":
 ##.WinReg.PutString(path'I am a default!')

   ⍝ * Read default value of subkey "path" (now defined!):
 →T.FailsIf'I am a default!'≢'?'##.WinReg.GetString path

   ⍝ * Unknown String: ("str1")
 →T.FailsIf'?'≢'?'##.WinReg.GetString path,'str1_'

   ⍝ * Known String: ("str2")
 →T.FailsIf'It is me!'≢'?'##.WinReg.GetString path,'str2'

   ⍝ Write really large string:
 ##.WinReg.PutString(path,'str1_')(100000⍴⍕⍳99999)
   ⍝ Read that big chunk back:
 →T.FailsIf(100000⍴⍕⍳99999)≢##.WinReg.GetString path,'str1_'

⍝ Tidy up
 ##.WinReg.DeleteSubKeyTree path

 R←T._OK
