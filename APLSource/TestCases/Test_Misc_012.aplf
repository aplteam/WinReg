 R←Test_Misc_012(stopFlag batchFlag);path;myReg;length;_;∆;⎕TRAP
⍝ Exercise `GetValue` & `PutValue` on DWORDs
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 path←GetRegPath

⍝ Preconditions
 ##.WinReg.DeleteSubKeyTree path                         ⍝ Delete any left-overs

⍝ Tests...
    ⍝ * Unknown Value: "val1"
 →T.FailsIf ¯1≢¯1 ##.WinReg.GetValue path,'val1'

    ⍝ * Write Value ("val2")
 ##.WinReg.PutValue(path,'val2')42
    ⍝ * Read back and check
 →T.FailsIf 42≢¯1 ##.WinReg.GetValue path,'val2'

    ⍝ * Write the biggest possible value
 ##.WinReg.PutValue(path,'val3')2147483647
    ⍝ * Read back and check
 →T.FailsIf 2147483647≢¯1 ##.WinReg.GetValue path,'val3'

    ⍝ * Write the smallest possible value
 ##.WinReg.PutValue(path,'val3')¯2147483647
    ⍝ * Read back and check
 →T.FailsIf ¯2147483647≢¯1 ##.WinReg.GetValue path,'val3'

⍝ Tidy up
 ##.WinReg.DeleteSubKeyTree path

 R←T._OK
